To run our program we need also startup file. Usually this file is written in asembler language.
I will try to explain each part of example file provided by STMicroelectronics for STM32f767 microcontroller.

```asm
  .syntax unified
  .cpu cortex-m7
  .fpu softvfp
  .thumb
```
- `.syntax unified` - means that we will not use specific assembly syntax. It is common in ARM to allow to use ARM and Thumb instruction set
- `.cpu cortex-m7` - this directive specifies target CPU
- `.fpu softvfp` - means that we will use software emulated floating-point unit, Keep in mind, that in STM32F7 we can use hardware one, but this directive is only for startup code where we are not using any floats.
- `.thumb` - we will use Thumb instruction set. They are more compact and allow to save some FLASH space.
`
```asm
.global  g_pfnVectors
.global  Default_Handler
```
- `.global` defines global symbol to use in other source files (including C/C++)

```asm
/* start address for the initialization values of the .data section. 
defined in linker script */
.word  _sidata
/* start address for the .data section. defined in linker script */  
.word  _sdata
/* end address for the .data section. defined in linker script */
.word  _edata
/* start address for the .bss section. defined in linker script */
.word  _sbss
/* end address for the .bss section. defined in linker script */
.word  _ebss
/* stack used for SystemInit_ExtMemCtl; always internal RAM used */
```
- Declaration of external variables. Those ones are defined in [[4. Linker script]]

```asm
    .section  .text.Reset_Handler
  .weak  Reset_Handler
  .type  Reset_Handler, %function
```
- `.section` defines section (see [[4. Linker script]]) where code should be placed
- `.weak` this attribute means that this definition of symbol `Reset_Handler` can (but don't have to!) be override somewhere else in application
- `.type` is information for debugger or can be used by linker (https://sourceware.org/binutils/docs/as/Type.html#Type)

```asm
Reset_Handler:  
  ldr   sp, =_estack      /* set stack pointer */

/* Copy the data segment initializers from flash to SRAM */  
  ldr r0, =_sdata
  ldr r1, =_edata
  ldr r2, =_sidata
  movs r3, #0
  b LoopCopyDataInit
```
- `Reset_Handler:` - label of function
- `ldr   sp, =_estack` - load value of `_estack` into `sp` processor register. Stack Pointer typically represents the top of the stack located at the end of the RAM memory
- `ldr r0, =_sdata` - load start address of `.data` section defined in [[4. Linker script]] into r0 core register
- `ldr r1, =_edata` - load end address of `.data` section
- `ldr r2, =_sidata` - load start address of `.data` initialization values which is located in FLASH memory
- `movs r3, #0` - clear r3 core register. It will be used as iterator for data copying
- `b LoopCopyDataInit` - branch directive - this instruction performs a jump to specified label `LoopCopyDataInit`

```asm
CopyDataInit:
  ldr r4, [r2, r3]
  str r4, [r0, r3]
  adds r3, r3, #4
```
- This section is responsible for copying one word from initialization data (FLASH) to variables in `.data` RAM section (variables that should be initialized by values other than zero)
- `ldr r4, [r2, r3]` - move word (4 bytes) into r4 register value from address r3 + r4
- `str r4, [r0, r3]` - store word from r4 register into address r3 + r4
- `adds r3, r3, #4` - increment r3 by 4

```asm
LoopCopyDataInit:
  adds r4, r0, r3
  cmp r4, r1
  bcc CopyDataInit
```
- `adds r4, r0, r3` - calculates next RAM address - add r0 to r3 and store result in r4
- `cmp r4, r1` - compare values in registers r4 and r1. Result of compare is stored by using Test Flags (NZCV) (https://developer.arm.com/documentation/107829/0200/Test-instructions--branching--and-loops). Compares r4 (current `.data` offset) with r1 (`_edata` address - end of `.data` section address)
- `bcc CopyDataInit` if left operand of `cmp` is lower than right one, then N flag will be set. This operation jumps into `CopyDataInit` label if N flag is set. So it will Copy next word if we didn't exceed `.data` section

```asm
/* Zero fill the bss segment. */
  ldr r2, =_sbss
  ldr r4, =_ebss
  movs r3, #0
  b LoopFillZerobss
```
- this section prepares addresses in registers to clear the `.bss` section (section with variables initialized by 0)

```asm
FillZerobss:
  str  r3, [r2]
  adds r2, r2, #4
```
- `str r3, [r2]` - store value from r3 register (zero) in address pointed by r2 register

```asm
LoopFillZerobss:
  cmp r2, r4
  bcc FillZerobss
```
- same as previously

```asm
/* Call the clock system initialization function.*/
  bl  SystemInit   
/* Call static constructors */
  bl __libc_init_array
/* Call the application's entry point.*/
  bl  main
  bx  lr
```
- `bl SystemInit` - in generated STM32 application there is a function responsible for initializing Clocks, PLL's and other modules. This command calls this function.
-  `bl __libc_init_array` - this command calls function responsible for initializing system standard libraries (libc)
- `bl main` - call main method
- `bx lr` - branch to the address in the link register (lr). This register holds return address after function execution.

```asm
    .section  .text.Default_Handler,"ax",%progbits
Default_Handler:
Infinite_Loop:
  b  Infinite_Loop
  .size  Default_Handler, .-Default_Handler
```
- `"ax"` - flag that inform, that this memory section is executable (https://developer.arm.com/documentation/101754/0622/armclang-Reference/armclang-Integrated-Assembler/Section-directives)
- `%progbits` - tells the linker, that this section contains program code
- `Default_Handler` - exception handler. It is called when user didn't provide own exception handling procedure
- `Infinite_Loop` - fallback function which is called in critical error
- `b Infinite_Loop` - jump back to the beginning of the infinite loop
- ` .size  Default_Handler, .-Default_Handler` tells liker what is the size of Default_Handler function

```asm
   .section  .isr_vector,"a",%progbits
  .type  g_pfnVectors, %object
  .size  g_pfnVectors, .-g_pfnVectors
```
- declaration of section placed in `.isr_vector` memory region
- `"a"` means that this section contains read only data (https://developer.arm.com/documentation/101754/0622/armclang-Reference/armclang-Integrated-Assembler/Section-directives)
- `  .size  g_pfnVectors, .-g_pfnVectors` is a size declaration of g_pfnVectors object. It is calculated by `.` - `g_pfnVectors`.

```asm
g_pfnVectors:
  .word  _estack
  .word  Reset_Handler

  .word  NMI_Handler
  .word  HardFault_Handler
  .word  MemManage_Handler
  .word  BusFault_Handler
  .word  UsageFault_Handler
  .word  0
  .word  0
  .word  0
  .word  0
  .word  SVC_Handler
  .word  DebugMon_Handler
  .word  0
  .word  PendSV_Handler
  .word  SysTick_Handler
  
  /* External Interrupts */
  .word     WWDG_IRQHandler                   /* Window WatchDog              */
  .word     PVD_IRQHandler                    /* PVD through EXTI Line detection */
  .word     TAMP_STAMP_IRQHandler             /* Tamper and TimeStamps through the EXTI line */
  .word     RTC_WKUP_IRQHandler               /* RTC Wakeup through the EXTI line */
  .word     FLASH_IRQHandler                  /* FLASH                        */
  .word     RCC_IRQHandler                    /* RCC                          */
  .word     EXTI0_IRQHandler                  /* EXTI Line0                   */
  .word     EXTI1_IRQHandler                  /* EXTI Line1                   */
  .word     EXTI2_IRQHandler                  /* EXTI Line2                   */
  .word     EXTI3_IRQHandler                  /* EXTI Line3                   */
  .word     EXTI4_IRQHandler                  /* EXTI Line4                   */
(...)
```
- declaration of Interrupt's vector table
- `  .word  _estack` first data in vector table is initial stack pointer value:
  "Stack pointer The Stack Pointer (SP) is register R13. In Thread mode, bit[1] of the CONTROL register indicates the stack pointer to use: • 0 = Main Stack Pointer (MSP). This is the reset value. • 1 = Process Stack Pointer (PSP). On reset, the processor loads the MSP with the value from address 0x00000000" (https://www.st.com/resource/en/programming_manual/pm0253-stm32f7-series-and-stm32h7-series-cortexm7-processor-programming-manual-stmicroelectronics.pdf page 21)
- First 15 interrupts are common in every ARM core (https://developer.arm.com/documentation/dui0646/b/BABIFJFG)
- Rest of interrupts are defined in reference manual for specific microcontroller that we used (https://www.st.com/resource/en/reference_manual/rm0410-stm32f76xxx-and-stm32f77xxx-advanced-armbased-32bit-mcus-stmicroelectronics.pdf page 314)

  ![](assets/Pasted%20image%2020240330005653.png)
  
```asm
   .weak      NMI_Handler
   .thumb_set NMI_Handler,Default_Handler
  
   .weak      HardFault_Handler
   .thumb_set HardFault_Handler,Default_Handler
  
   .weak      MemManage_Handler
   .thumb_set MemManage_Handler,Default_Handler
  
   .weak      BusFault_Handler
   .thumb_set BusFault_Handler,Default_Handler

   .weak      UsageFault_Handler
   .thumb_set UsageFault_Handler,Default_Handler

   .weak      SVC_Handler
   .thumb_set SVC_Handler,Default_Handler
```
- `.weak` defines definition as weak - it means that it will be replaced if stronger definition will be find during linking process. 
- `.thumb_set` this sets address of specific handler to point to Default_Handler. It is used when user didn't define own implementation of this handler.


## Time to compile
We need to provide our startup file to build process.

First we need to create rule for compiling assembly files (*.s):
```patch
 %.o: %.c
 	$(CC) -c $(CFLAGS) $< -o $@
 
+%.o: %.s
+	$(CC) -c $(ASFLAGS) $< -o $@
+
```

We will use separated flags:
```patch
 CFLAGS=
+ASFLAGS=
```

Now we can add [startup_stm32f767xx.s](../startup_stm32f767xx.s) file to build process:
```patch
-main.bin: main.o syscalls.o
-	$(CC) $(LDFLAGS) main.o syscalls.o -o main.elf
+main.bin: main.o syscalls.o startup_stm32f767xx.o
+	$(CC) $(LDFLAGS) main.o syscalls.o startup_stm32f767xx.o -o main.elf
```

Try to build project:
```powershell
PS E:\Projects\STM32\stm32f767 clean project> make clean; make
rm -f *.o *.elf *.bin
arm-none-eabi-gcc -c  main.c -o main.o
arm-none-eabi-gcc -c  syscalls.c -o syscalls.o
arm-none-eabi-gcc -c  startup_stm32f767xx.s -o startup_stm32f767xx.o
arm-none-eabi-gcc -specs=nosys.specs -TSTM32F767ZITx_FLASH.ld main.o syscalls.o startup_stm32f767xx.o -o main.elf
C:/ST/STM32CubeCLT/GNU-tools-for-STM32/bin/../lib/gcc/arm-none-eabi/11.3.1/../../../../arm-none-eabi/bin/ld.exe: startup_stm32f767xx.o: in function `LoopFillZerobss':
(.text.Reset_Handler+0x2a): undefined reference to `SystemInit'
collect2.exe: error: ld returned 1 exit status
make: *** [makefile:14: main.bin] Error 1
```

We have undefined reference:
- `SystemInit` - this function should be provided by user and is used to pre-init critical microcontroller peripherals. We will discuss it later

For now it will be enough just to define `void SystemInit(void)` function in our [main.c](../main.c) file before `void main(void)`:
```patch
+void SystemInit(void)
+{
+
+}
+
 void main(void)
```

Try to build project again:
```powershell
PS E:\Projects\STM32\stm32f767 clean project> make clean; make
rm -f *.o *.elf *.bin
arm-none-eabi-gcc -c  main.c -o main.o
arm-none-eabi-gcc -c  syscalls.c -o syscalls.o
arm-none-eabi-gcc -c  startup_stm32f767xx.s -o startup_stm32f767xx.o
arm-none-eabi-gcc -specs=nosys.specs -TSTM32F767ZITx_FLASH.ld main.o syscalls.o startup_stm32f767xx.o -o main.elf
arm-none-eabi-objcopy -O binary main.elf main.bin
```

## Conclusion
Now our project is able to build.

In next chapter we will discuss about some important build flags.